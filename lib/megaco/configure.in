
define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl

AC_INIT(vsn.mk)

if test -z "$ERL_TOP" || test ! -d $ERL_TOP ; then
  AC_CONFIG_AUX_DIRS(autoconf)
else
  erl_top=${ERL_TOP}
  AC_CONFIG_AUX_DIRS($erl_top/erts/autoconf)
fi

if test "X$host" != "Xfree_source" -a "X$host" != "Xwin32"; then
    AC_CANONICAL_HOST
else
    host_os=win32
fi


AC_ARG_ENABLE(threads,
[  --enable-threads        use threads],
[ case "$enableval" in
    no) enable_threads=no ;;
    *)  enable_threads=yes ;;
  esac ], enable_threads=no)
 

dnl ----------------------------------------------------------------------
dnl Checks for programs.
dnl ----------------------------------------------------------------------


dnl
dnl flex is needed by megaco. lex wont do!
dnl

AC_PROG_LEX
if test "$LEX" != flex; then
  ENABLE_MEGACO_FLEX_SCANNER=false
else
  ENABLE_MEGACO_FLEX_SCANNER=true
fi
AC_SUBST(ENABLE_MEGACO_FLEX_SCANNER)


dnl
dnl For increased performance it is possible to disable lineno
dnl
AC_ARG_ENABLE(megaco_flex_scanner_lineno,
[  --disable-megaco-flex-scanner-lineno  disable megaco flex scanner lineno],
  if test x${enable_megaco_flex_scanner_lineno} = xno ; then
    ENABLE_MEGACO_FLEX_SCANNER_LINENO=false
  else
    ENABLE_MEGACO_FLEX_SCANNER_LINENO=true
  fi, 
  ENABLE_MEGACO_FLEX_SCANNER_LINENO=true)
AC_SUBST(ENABLE_MEGACO_FLEX_SCANNER_LINENO)



dnl
dnl C compiler (related) defs
dnl

AC_PROG_CC

dnl Magic test for clearcase.
if test -d ../../system; then
	OTP_EXTRA_FLAGS=-DOTP_RELEASE
else
	OTP_EXTRA_FLAGS=
fi


dnl
dnl Flags to the C compiler
dnl
dnl make sure we find config.h
CFLAGS="$CFLAGS -I${ERL_TOP}/erts/$host $OTP_EXTRA_FLAGS"

if test "X$host" = "Xwin32"; then
  DED_CFLAGS="$CFLAGS"
else
  case $host_os in
    darwin*)
	CFLAGS="$CFLAGS -no-cpp-precomp"
	;;
  esac

  if test "$enable_threads" = yes; then
        DED_CFLAGS="-D_THREAD_SAFE -D_REENTRANT"
  fi

  if test "x$GCC" = xyes; then
	DED_CFLAGS="$CFLAGS -fPIC $DED_CFLAGS"
  else
	DED_CFLAGS="$CFLAGS $DED_CFLAGS"
  fi
fi

AC_SUBST(DED_CFLAGS)


AC_CHECK_PROGS(DED_LD, [ld.sh ld], no_ld)
if test "$DED_LD" = no_ld; then
  AC_MSG_ERROR([ld is required to build the flex scanner!])
fi


AC_MSG_CHECKING(for linker flags for loadable drivers)
case $host_os in
	win32)
		DED_LDFLAGS="-dll"
	;;
	solaris2*|sysv4*)
		DED_LDFLAGS="-G"
	;;
	aix4*)
		DED_LDFLAGS="-G -bnoentry -bexpall"
	;;
	freebsd2*)
		# Non-ELF GNU linker
		DED_LDFLAGS="-Bshareable"
	;;
	darwin*)
		# Mach-O linker, a shared lib and a loadable
		# object file is not the same thing.
		DED_LDFLAGS="-bundle -flat_namespace -undefined suppress"
		DED_CFLAGS="$DED_CFLAGS -fno-common"
	;;
	*)
		# assume GNU linker and ELF
		DED_LDFLAGS="-shared"
	;;
esac
AC_MSG_RESULT([$DED_LDFLAGS])
AC_SUBST(DED_LDFLAGS)


AC_CHECK_PROG(PERL, perl, perl, no_perl)
if test "$PERL" = no_perl; then
  AC_MSG_ERROR([Perl is required to build the flex scanner!])
fi


dnl This is the os flavour, should be unix, vxworks or win32
if test "X$host" = "Xwin32"; then
	ERLANG_OSTYPE=win32
else
	ERLANG_OSTYPE=unix
fi

AC_SUBST(ERLANG_OSTYPE)


AC_OUTPUT(src/flex/$host/Makefile:src/flex/Makefile.in)

